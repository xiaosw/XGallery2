apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
//    buildToolsVersion "24.0.1"
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.xiaosw.gallery"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            manifest {
                // there's only ever one file so srcFile replaces it.
                srcFile 'src/main/AndroidManifest.xml'
            }

            jniLibs.srcDirs = ['src/main/libs']

            java {
                srcDirs = ['src/main/java', '../mp4parser/isoparser/src/main/java', '../Gallery2/gallerycommon/src', '../Gallery2/src_pd']
            }

            res {
                srcDirs = ['src/main/res']
            }
            assets {
                srcDirs = ['src/main/assets']
            }
            resources {
                srcDirs = ['src/main/java', '../Gallery2/mp4parser/isoparser/src/main/resources']
            }
            aidl {
                srcDirs = ['src/main/java']
            }
            renderscript {
                srcDirs = ['src/main/java']
            }
        }
    }

    productFlavors {
        dev {
            versionName defaultConfig.versionName + '-dev'
        }

        innerTest {
            versionName = defaultConfig.versionName + '-test'
        }

        online {
            versionName = defaultConfig.versionName + '-online'
        }
    }

    applicationVariants.all {
        variant ->
            variant.outputs.each {
                output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        if (variant.flavorName == 'innerTest' && variant.buildType.name == 'release') {
                            def newFileName = 'app-innerTest-unsigned.apk'
                            output.outputFile = new File(outputFile.parent, newFileName)
                        }
                        if (variant.flavorName == 'online' && variant.buildType.name == 'release') {
                            def newFileName = 'app-online-unsigned.apk'
                            output.outputFile = new File(outputFile.parent, newFileName)
                        }
                    }
            }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:support-v13:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:cardview-v7:23.0.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile files('src/main/libs/nineoldandroids-2.4.0.jar')
    compile files('src/main/libs/httpclient-4.5.2.jar')
    compile files('src/main/libs/httpcore-4.4.4.jar')
    compile files('src/main/libs/org.apache.http.legacy.jar')
    compile files('src/main/libs/xmp_toolkit.jar')
}
